<!DOCTYPE html>
<html>
<HEAD>
<TITLE>EXAM Answer #5</TITLE>

</HEAD>
<BODY>
<strong>
<pre>

#!/bin/csh -f

if(x${1} != x) then
   gpmap << SELECT1
      garea = $1
      e
SELECT1
else
  gpmap << SELECT2
     garea = usnps
     proj = str/90;-100;0 
     map = 6
     title = 5/-2/Select Map Area
     dev = xw|SELECT_AREA
     clear = y
     \$mapfil = hipowo.gsf
     r

     cursor garea
     e
SELECT2
  gpend
endif

# This is the directory of our lightning data
set NLDN=$GEMDATA/nldn

# set the current year, month day
set YMD=`date -u '+%Y%m%d'`

# get the day of week, month abbrev., day and 4 digit year for title
set TITLE = `date -u '+%A %B %d, %Y'`
set INC = 1;

set DEVICE = 'gf'
#set DEVICE = 'xw'

# we'll start at hour 0 and loop through hour 23
@ HOUR = 0
@ STOP_HOUR = 23

@ COLOR=30


while($HOUR <= $STOP_HOUR)

   if($HOUR < 10) then
      set SFHOUR=0${HOUR}
   else
      set SFHOUR=${HOUR}
   endif 

   set FILES=`ls $NLDN/${YMD}${SFHOUR}*_nldn.gem`
   if($#FILES > 0) then

   foreach FILE ($FILES)
      echo "plotting $FILE"
      set TIMES=`sfctime $FILE`

      foreach TIME ($TIMES)
      set CLEAR='no'

      set EXT=''
      set DEV=${DEVICE}
      if($DEVICE == gf) then
         set EXT='.gif'
         set CLEAR='yes'
         set DEV = ${DEVICE}'|lightning_'${YMD}'_'${INC}${EXT}
      endif

      sfmap << EOF
      map = 1
      dev = ${DEV}
      area = dset
      filter = no
      clear = $CLEAR
      text = 1.2/23/1/hw
      title = 5/-2/Lightning Strikes  $TITLE ${TIME}Z
      panel = .02;.02;.97;.97/1/1/4
      sffile = $FILE
      color = 0;${COLOR}
      dattim = $TIME
      sfparm = sgnl<-400;mark:22:1:1
      r
  
      clear = no 
      sfparm = sgnl>400;mark:1:1:1
      r

      sfparm = sgnl > -401 < 401;mark:15:2:1
      r
   
      e
EOF
      set CLEAR = no
      @ INC = $INC + 1
      end # foreach TIME
   end # foreach file
   endif

   @ HOUR = $HOUR + 1
   @ COLOR = $COLOR - 1
   if($DEVICE == gf) then
      gpend
   endif
end



exit(0)
</pre>
</strong>
      

 
<p>
<img src="../images/bluehorizontaline.gif">
<pre>
<a href="../exam.html#5"><img alt="previous page" src="../images/prev.gif"></a>

<script src="/gempak/add_in.js" type="text/javascript"></script>
</body>
</html>
