dnl Process this file with autoconf to produce a configure script.
AC_INIT(rsl, v1.36)
AC_CONFIG_SRCDIR(volume.c)

AM_INIT_AUTOMAKE
AM_CONFIG_HEADER(config.h)

dnl Default for GVS and friends.
AC_PREFIX_DEFAULT(/usr/local/trmm/GVBOX)

dnl Checks for programs.
AC_PROG_CC
AM_PROG_LIBTOOL
AC_PROG_YACC
AM_PROG_LEX
AC_PROG_LN_S
AC_PROG_RANLIB

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h malloc.h strings.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_STRUCT_TM

dnl Checks for library functions.
dnl AC_FUNC_SETVBUF_REVERSED
AC_CHECK_FUNCS(mktime strdup strstr)

dnl I would like lassen to be defined.  Override this in config.h.
AC_DEFINE(HAVE_LASSEN, 1,
    [For LASSEN capability.  Change this to '#undef HAVE_LASSEN', if you
     don't want LASSEN.])

dnl Checks for libraries.
if test $prefix = NONE; then
  prefix=$ac_default_prefix
fi
LIBDIR="-L$prefix/lib"
LIBS="-lz -lm"

# The order of the libraries is important.
# This works:
#   -lrsl -limplode -letor -ltsdistk -lmfhdf -ldf -ljpeg -lz -lm
#
# If mounting with shared libraries we may have to specify all possible libraries.
# First check with simple LIBS list, if it fails, then expand the list.

AC_CHECK_LIB(implode,  _implode,           ,,$LIBDIR)
AC_CHECK_LIB(jpeg,     jpeg_CreateCompress,,,$LIBDIR)
AC_CHECK_LIB(df,       DFopen,             ,,$LIBDIR)
AC_CHECK_LIB(mfhdf,    SDstart,            ,,$LIBDIR)
AC_CHECK_LIB(tsdistk,  TKopen,             ,,$LIBDIR)

# Because -letor may depend on RSL being installed, just check for
# the library libetor.a in a couple of places.

dnl For autoheader, generate template for HAVE_LIBETOR.
AH_TEMPLATE(HAVE_LIBETOR,[For EDGE capability.  Change this
    to '#define HAVE_LIBETOR 1', if you have libetor -- the EDGE decoding
    library.])

L=""

if test "$L" = ""; then # Check for the file directly.
	AC_CHECK_FILE($prefix/lib/libetor.a, L="-letor -lrsl -letor"; AC_DEFINE(HAVE_LIBETOR))
fi
if test "$L" = ""; then # Check for the file directly.
	AC_CHECK_FILE(/usr/local/lib/libetor.a, L="-letor -lrsl -letor"; AC_DEFINE(HAVE_LIBETOR))
fi

if test "$L" != ""; then # libetor exists.
LIBS="$LIBDIR $L $LIBS"
else
LIBS="$LIBDIR $LIBS"
fi

# Check if yywrap is defined in toolkit, and add $LEXLIB to LIBS if not.
YYWRAP=""
AC_CHECK_LIB(tsdistk,  yywrap, YYWRAP="yes",,$LIBDIR)
if test "$YYWRAP" = ""; then
	LIBS="$LIBS $LEXLIB"
fi

AC_MSG_RESULT(LIBS = $LIBS)
AC_CONFIG_FILES([Makefile colors/Makefile doc/Makefile examples/Makefile])
AC_OUTPUT
